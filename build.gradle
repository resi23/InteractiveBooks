plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

tasks.compileJava.options.encoding = 'UTF-8'
tasks.compileJava.sourceCompatibility = JavaVersion.VERSION_1_8

group = 'net.leonardo_dgs'
version = '1.7.0'
description = 'Create cool interactive books!'

def mainPackage = 'net.leonardo_dgs.interactivebooks'

repositories {
    mavenCentral()

    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.19-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    implementation 'de.tr7zw:item-nbt-api:2.11.2'
    implementation 'net.kyori:adventure-text-minimessage:4.13.1'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.0'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'com.github.Simplix-Softworks:SimplixStorage:3.2.5'
}

tasks.shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set('')
    archiveVersion.set('')
    minimize()

    relocate 'de.tr7zw.changeme.nbtapi', mainPackage + '.lib.nbtapi'
    relocate 'net.kyori.adventure', mainPackage + '.lib.adventure'
    relocate 'net.kyori.examination', mainPackage + '.lib.examination'
    relocate 'co.aikar.commands', mainPackage + '.lib.acf'
    relocate 'co.aikar.locales', mainPackage + '.lib.locales'
    relocate 'de.leonhard.storage', mainPackage + '.lib.simplixstorage'
    relocate 'org.intellij', mainPackage + '.lib.intellij'
    relocate 'org.jetbrains', mainPackage + '.lib.jetbrains'
    relocate 'org.bstats', mainPackage + '.bstats'
}

processResources {
    expand(
            version: project.version,
            description: project.description,
            author: 'Leonardo_DGS',
            website: 'https://www.spigotmc.org/resources/45604/'
    )
}

tasks.jar.enabled = false

artifacts {
    archives shadowJar
}
